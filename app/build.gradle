buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        //find the latest io.fabric.tools:gradle here: https://docs.fabric.io/android/changelog.html#fabric-gradle-plugin
        classpath 'io.fabric.tools:gradle:1.28.0'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder. Copied from: https://developer.android.com/studio/publish/app-signing.html#secure-shared-keystore
def keystorePropertiesFile = rootProject.file("../MyHitchhikingSpots-private/keystore.properties")
def otherPrivatePropertiesFile = rootProject.file("../MyHitchhikingSpots-private/other-private.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()
def otherPrivateProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
otherPrivateProperties.load(new FileInputStream(otherPrivatePropertiesFile))

def minSdk = hasProperty('minSdk') ? minSdk : 15

ext {
    androidxVersion = '1.0.0'
}

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        vectorDrawables.useSupportLibrary = true
        manifestPlaceholders = [fabricApiKey: otherPrivateProperties['fabricApiKey']]
        applicationId 'com.myhitchhikingspots'
        minSdkVersion minSdk
        targetSdkVersion 26
        versionCode 28
        versionName "2.2.6"
        // Stops the Gradle pluginâ€™s automatic rasterization of vectors
        generatedDensities = []
        // Enabling multidex support.
        multiDexEnabled true
        signingConfig signingConfigs.config
    }

    // Flag to tell aapt to keep the attribute ids around
    aaptOptions {
        additionalParameters "--no-version-vectors"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    productFlavors {
    }
    sourceSets { main { res.srcDirs = ['src/main/res', 'src/main/res/values-pt'] } }
    dataBinding.enabled = true

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://maven.google.com/" }
}

dependencies {
    //Begin: Dependencies for MapBox
    // add the Mapbox SDK dependency below
    implementation('com.mapbox.mapboxsdk:mapbox-android-sdk:7.3.2')

    //Mapbox plugins
    implementation 'com.mapbox.mapboxsdk:mapbox-android-plugin-localization-v7:0.10.0-SNAPSHOT'

    //End: Dependencies for MapBox
    implementation 'cz.msebera.android:httpclient:4.4.1.1'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.4@aar') {
        transitive = true;
    }
    implementation 'org.greenrobot:greendao:3.2.0'
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    implementation 'joda-time:joda-time:2.9.2'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.github.florent37:viewtooltip:1.1.5'

    implementation "androidx.appcompat:appcompat:${androidxVersion}"
    implementation "androidx.recyclerview:recyclerview:${androidxVersion}"
    implementation "com.google.android.material:material:${androidxVersion}"
    implementation "androidx.cardview:cardview:${androidxVersion}"

    implementation "com.squareup.picasso:picasso:2.5.2"
    testImplementation "junit:junit:4.13-beta-2"
    testImplementation "com.google.truth:truth:0.44"
    implementation "com.google.firebase:firebase-core:16.0.9"
}
